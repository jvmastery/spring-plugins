plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    // 版本配置
    group = project.ext.PROJECT_INFO_GROUP
    version = project.ext.PROJECT_INFO_VERSION

    // 通用编译配置
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.jar {
        // 移除默认的 `-plain` 后缀，否则maven项目无法正常下载依赖
        archiveClassifier.set('')
    }

    plugins.withId('org.springframework.boot') {
        tasks.named('bootJar') {
            enabled = false // 如果子模块是 Starter，禁用 bootJar
        }

        tasks.named('jar') {
            enabled = true
        }
    }

    // 仓库配置
    repositories {
    		maven { url "https://maven.aliyun.com/repository/public" }
    		mavenCentral()
    }

    // 使用 dependencyManagement 管理依赖的版本

    dependencyManagement {
        imports {

        }
    }

    // 公共依赖管理
    dependencies {
    //    compileOnly "org.projectlombok:lombok:${project.ext.LOMBOK_VERSION}"
    //    annotationProcessor "org.projectlombok:lombok:${project.ext.LOMBOK_VERSION}"
        implementation "com.fasterxml.jackson.core:jackson-databind:${project.ext.JSON_VERSION}"
        implementation "com.fasterxml.jackson.core:jackson-core:${project.ext.JSON_VERSION}"
        implementation "com.fasterxml.jackson.core:jackson-annotations:${project.ext.JSON_VERSION}"
        testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    }

    // 通用发布配置
    publishing {
        publications {
            mavenJava(MavenPublication) {
                //version = project.ext.PROJECT_INFO_VERSION
                from components.java
            }
        }

        repositories {
            maven {
                name = "local"
                url = uri("D:\\maven\\repository") // 本地仓库
            }

            maven {
                name = "aliyun-snapshot"
                url = uri("https://packages.aliyun.com/maven/repository/2005963-snapshot-frHnmh/")
                credentials {
                    username = findProperty("MAVEN_REPO_USER_NAME")
                    password = findProperty("MAVEN_REPO_PASSWORD")
                }
            }

            maven {
                name = "aliyun-release"
                url = uri("https://packages.aliyun.com/5ed5a315d1d1abe63b55e3ee/maven/2005963-release-HwEidZ")
                credentials {
                    username = findProperty("MAVEN_REPO_USER_NAME")
                    password = findProperty("MAVEN_REPO_PASSWORD")
                }
            }
        }
    }
}



tasks.withType(JavaExec) {
    standardOutput = System.out
    errorOutput = System.err
    ignoreExitValue = true // 避免非零退出码影响构建流程
}

task publishAll {
    dependsOn subprojects*.publish
}
